% Notion of an expression being closed under a given environment.
rclosed : var-list -> rexp -> type.

rclosed/num : rclosed Delta (rnum N).

rclosed/var : rclosed Delta (rvar X)
              <- trans-rb-var Delta X I.

rclosed/lam : rclosed Delta (rlam [x] R x)
              <- {x} rclosed (var-list/cons Delta x) (R x).

rclosed/app : rclosed Delta (rapp R1 R2)
              <- rclosed Delta R1
              <- rclosed Delta R2.



% Totality of translation for rclosed terms.
trans-rb-exists : rclosed Delta R -> trans-rb Delta R B -> type.
%mode trans-rb-exists +DC -DT.

trans-rb-exists/num : trans-rb-exists rclosed/num trans-rb/num.

trans-rb-exists/var : trans-rb-exists (rclosed/var DTV) (trans-rb/var DTV).

trans-rb-exists/lam : trans-rb-exists (rclosed/lam DC) (trans-rb/lam DT)
                    <- {x} trans-rb-exists (DC x) (DT x).

trans-rb-exists/app : trans-rb-exists (rclosed/app DC2 DC1) (trans-rb/app DT2 DT1)
                    <- trans-rb-exists DC1 DT1
                    <- trans-rb-exists DC2 DT2.

%worlds (bl-var) (trans-rb-exists _ _).
%total DC (trans-rb-exists DC _).
