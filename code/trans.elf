rtrans : exp -> rexp -> type.

rtrans/num : rtrans (num N) (rnum N).

rtrans/lam : rtrans (lam E) (rlam R)
              <- {x} {y} rtrans x (rvar y) -> rtrans (E x) (R y).

rtrans/app : rtrans (app E1 E2) (rapp R1 R2)
              <- rtrans E1 R1
              <- rtrans E2 R2.



% Ordered lists of variables.
venv : type. %name venv Delta.

venv/nil : venv.
venv/cons : venv -> exp -> venv.



% Translation of variables.
trans-var : venv -> exp -> nat -> type.

trans-var/here : trans-var (venv/cons Delta X) X z.

trans-var/there : trans-var (venv/cons Delta Y) X (s I)
                   <- trans-var Delta X I.



% Translation of expressions.
trans : venv -> exp -> bexp -> type.

trans/num : trans Delta (num N) (bnum N).

trans/var : trans Delta E (bvar I)
             <- trans-var Delta E I.

trans/lam : trans Delta (lam [x] E x) (blam B)
             <- {x} trans (venv/cons Delta x) (E x) B.

trans/app : trans Delta (app E1 E2) (bapp B1 B2)
             <- trans Delta E1 B1
             <- trans Delta E2 B2.
