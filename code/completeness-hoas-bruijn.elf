completeness-hb'-var : eval E C -> trans-hb-var Alph I E -> beval-var Alph I U -> cor U C -> type.
%mode completeness-hb'-var +DE +DTV -DBV -DC.

completeness-hb'-var/here/num : completeness-hb'-var eval/num (trans-hb-var/here DC) beval-var/here DC.

completeness-hb'-var/here/fun : completeness-hb'-var eval/lam (trans-hb-var/here DC) beval-var/here DC.

completeness-hb'-var/there : completeness-hb'-var DE (trans-hb-var/there DTV) (beval-var/there DBV) DC
                              <- completeness-hb'-var DE DTV DBV DC.

%worlds () (completeness-hb'-var _ _ _ _).
%total (DTV) (completeness-hb'-var _ DTV _ _).



s-compat-cor : cor (vnum N1) (num N2) -> cor (vnum (s N1)) (num (s N2)) -> type.
%mode s-compat-cor +DC -DC'.

- : s-compat-cor cor/num cor/num.

%worlds () (s-compat-cor _ _).
%total DC (s-compat-cor DC _).



s-inj-cor : cor (vnum (s N1)) (num (s N2)) -> cor (vnum N1) (num N2) -> type.
%mode s-inj-cor +DC -DC'.

- : s-inj-cor cor/num cor/num.

%worlds () (s-inj-cor _ _).
%total DC (s-inj-cor DC _).



completeness-hb' : eval E C -> trans-hb Alph B E -> beval Alph B U -> cor U C -> type.
%mode completeness-hb' +DE +DT -DB -DC.

completeness-hb'/num : completeness-hb' eval/num trans-hb/num beval/num cor/num.

completeness-hb'/var : completeness-hb' DE (trans-hb/var DTV) (beval/var DBV) DC
                        <- completeness-hb'-var DE DTV DBV DC.

completeness-hb'/lam : completeness-hb' eval/lam (trans-hb/lam DT1) beval/lam (cor/fun (trans-hb/lam DT1)).

completeness-hb'/app : completeness-hb' (eval/app DE3 DE2 DE1) (trans-hb/app DT2 DT1) (beval/app DB3 DB2 DB1) DC3
                        <- completeness-hb' DE1 DT1 DB1 (cor/fun (trans-hb/lam [v] [x] [dc] DT v x dc))
                        <- completeness-hb' DE2 DT2 DB2 (DC2 : cor U2 C2)
                        <- completeness-hb' DE3 (DT U2 C2 DC2) DB3 DC3.

completeness-hb'/suc : completeness-hb' (eval/suc DE1) (trans-hb/suc DT1) (beval/suc DB1) DC
                        <- completeness-hb' DE1 DT1 DB1 DC1
                        <- s-compat-cor DC1 DC.

%worlds () (completeness-hb' _ _ _ _).
%total (DE) (completeness-hb' DE _ _ _).



coerce-beval : cor (vnum N1) (num N2) -> beval Alph B (vnum N1) -> beval Alph B (vnum N2) -> type.
%mode coerce-beval +DC +DB -DB'.

- : coerce-beval cor/num DB DB.

%worlds () (coerce-beval _ _ _).
%total (DC) (coerce-beval DC _ _).



completeness-hb : eval E (num N) -> trans-hb env/nil B E -> beval env/nil B (vnum N) -> type.
%mode completeness-hb +DE +DT -DB.

completeness-hb/ : completeness-hb DE DT DB'
                    <- completeness-hb' DE DT DB DC
                    <- coerce-beval DC DB DB'.

%worlds () (completeness-hb _ _ _).
%total (DE) (completeness-hb DE _ _).
