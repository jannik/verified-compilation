% Translation of values.
cor : value -> exp -> type. %name cor DC.
%mode cor +V -C.

% Translation of variables.
trans-hb-var : store -> nat -> exp -> type. %name trans-hb-var DTV.
%mode trans-hb-var +Sigma +I -C.

% Translation between HOAS and de Bruijn.
trans-hb : store -> bexp -> exp -> type. %name trans-hb DT.
%mode trans-hb +Sigma +B -E.



% rules for cor

cor/num : cor (vnum N) (num N).

cor/fun : cor (vclos Sigma B) (lam E)
           <- trans-hb Sigma (blam B) (lam E).



% rules for trans-hb-var

trans-hb-var/here : trans-hb-var (store/cons Sigma V) z C
                     <- cor V C.

trans-hb-var/there : trans-hb-var (store/cons Sigma V') (s I) C
                      <- trans-hb-var Sigma I C.



% rules for trans-hb

trans-hb/num : trans-hb Sigma (bnum N) (num N).

trans-hb/var : trans-hb Sigma (bvar I) E
                <- trans-hb-var Sigma I E.

trans-hb/lam : trans-hb Sigma (blam B) (lam E)
                <- ({v : value} {x : exp}
                      cor v x -> trans-hb (store/cons Sigma v) B (E x)).

trans-hb/app : trans-hb Sigma (bapp B1 B2) (app E1 E2)
                <- trans-hb Sigma B1 E1
                <- trans-hb Sigma B2 E2.
