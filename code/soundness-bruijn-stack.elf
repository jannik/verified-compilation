eq-value : value -> value -> type.

eq-value/refl : eq-value V V.



eq-svalue : svalue -> svalue -> type.

eq-svalue/refl : eq-svalue W W.



invert-ssteps-suc : ssteps (frames/cons Xi Tau (sprog/cons ssuc P)) (wstack/cons Psi W) frames/nil Psi'
                 -> eq-svalue W (wnum N)
                 -> ssteps (frames/cons Xi Tau P) (wstack/cons Psi (wnum (s N))) frames/nil Psi'
                 -> type.
%mode invert-ssteps-suc +DP -DQ1 -DP'.

invert-ssteps-suc/ : invert-ssteps-suc (ssteps/more DP sstep/suc) eq-svalue/refl DP.

%worlds () (invert-ssteps-suc _ _ _).
%reduces DP' < DP (invert-ssteps-suc DP _ DP').
%total {} (invert-ssteps-suc _ _ _).



invert-cor-bs-num : cor-bs V W -> eq-svalue W (wnum N) -> eq-value V (vnum N) -> type.
%mode invert-cor-bs-num +DC +DQW -DQV.

invert-cor-bs-num/ : invert-cor-bs-num cor-bs/num eq-svalue/refl eq-value/refl.

%worlds () (invert-cor-bs-num _ _ _).
%total {} (invert-cor-bs-num _ _ _).



invert-cor-bs-fun : cor-bs V W -> eq-svalue W (wclos Tau P) -> eq-value V (vclos Sigma B) -> trans-bs B P -> comp-bs Sigma Tau -> type.
%mode invert-cor-bs-fun +DC +DQW -DQV -DT -DD.

invert-cor-bs-fun/ : invert-cor-bs-fun (cor-bs/fun DT DD) eq-svalue/refl eq-value/refl DT DD.

%worlds () (invert-cor-bs-fun _ _ _ _ _).
%total {} (invert-cor-bs-fun _ _ _ _ _).



invert-ssteps-app : ssteps (frames/cons Xi Tau (sprog/cons sapp P)) (wstack/cons (wstack/cons Psi W1) W2) frames/nil Psi'
                 -> eq-svalue W1 (wclos Tau' P') -> ssteps (frames/cons (frames/cons Xi Tau P) (sstore/cons Tau' W2) P') Psi frames/nil Psi' -> type.
%mode invert-ssteps-app +DP -DQ -DP'.

invert-ssteps-app/ : invert-ssteps-app (ssteps/more DP sstep/app) eq-svalue/refl DP.

%worlds () (invert-ssteps-app _ _ _).
%reduces DP' < DP (invert-ssteps-app DP _ DP').
%total {} (invert-ssteps-app _ _ _).



coerce-beval : beval Sigma B V -> eq-value V V' -> beval Sigma B V' -> type.
%mode coerce-beval +DB +DQV -DB'.

coerce-beval/ : coerce-beval DB eq-value/refl DB.

%worlds () (coerce-beval _ _ _).
%total {} (coerce-beval _ _ _).



soundness-bs-var : seval-var Tau I W -> comp-bs Sigma Tau -> beval-var Sigma I V -> cor-bs V W -> type.
%mode soundness-bs-var +DSV +DD -DBV -DC.

soundness-bs-var/here : soundness-bs-var seval-var/here (comp-bs/cons DC DD) beval-var/here DC.

soundness-bs-var/there : soundness-bs-var (seval-var/there DSV) (comp-bs/cons DC' DD) (beval-var/there DBV) DC
                          <- soundness-bs-var DSV DD DBV DC.

%worlds () (soundness-bs-var _ _ _ _).
%total DSV (soundness-bs-var DSV _ _ _).



soundness-bs' : ssteps (frames/cons Xi Tau P1-P2) Psi frames/nil (wstack/cons wstack/nil W') -> trans-bs-acc B P2 P1-P2 -> comp-bs Sigma Tau
             -> beval Sigma B V -> cor-bs V W -> ssteps (frames/cons Xi Tau P2) (wstack/cons Psi W) frames/nil (wstack/cons wstack/nil W') -> type.
%mode soundness-bs' +DP +DT +DD -DB -DC -DP'.

soundness-bs'/num : soundness-bs' (ssteps/more DP sstep/num) trans-bs-acc/num DD beval/num cor-bs/num DP.

soundness-bs'/var : soundness-bs' (ssteps/more DP (sstep/var DSV)) trans-bs-acc/var DD (beval/var DBV) DC DP
                     <- soundness-bs-var DSV DD DBV DC.

soundness-bs'/lam : soundness-bs' (ssteps/more DP sstep/lam) (trans-bs-acc/lam DT1) DD beval/lam (cor-bs/fun DT1 DD) DP.

soundness-bs'/app : soundness-bs' DP (trans-bs-acc/app DT1 DT2) DD (beval/app DB3 DB2 DB1') DC DP4
                     <- soundness-bs' DP DT1 DD DB1 DC1 DP1
                     <- soundness-bs' DP1 DT2 DD DB2 DC2 DP2
                     <- invert-ssteps-app DP2 DQW DP3
                     <- invert-cor-bs-fun DC1 DQW DQV DT3 DD'
                     <- coerce-beval DB1 DQV DB1'
                     <- soundness-bs' DP3 DT3 (comp-bs/cons DC2 DD') DB3 DC (ssteps/more DP4 sstep/ret).

soundness-bs'/suc : soundness-bs' DP (trans-bs-acc/suc DT1) DD (beval/suc DB1') cor-bs/num DP1'
                     <- soundness-bs' DP DT1 DD DB1 DC1 DP1
                     <- invert-ssteps-suc DP1 DQW DP1'
                     <- invert-cor-bs-num DC1 DQW DQV
                     <- coerce-beval DB1 DQV DB1'.

%{
soundness-bs'/case : soundness-bs' DP (trans-bs-acc/case DT1 DT2 DT3) DD (DB' _ DB2) DC2 DP2
                      <- soundness-bs' DP DT1 DD DB1 DC1 DP1
                      <- soundness-bs'-lemma DB1 DC1 DP1 DT2 DT3 DD DP1' DT' DD' DB'
                      <- soundness-bs' DP1' DT' DD' DB2 DC2 (ssteps/more DP2 sstep/ret).
}%

%worlds () (soundness-bs' _ _ _ _ _ _).
%reduces DP' < DP (soundness-bs' DP _ _ _ _ DP').
%total {DP DT} (soundness-bs' DP DT _ _ _ _).



coerce-cor-bs : sstep (frames/cons Xi Tau sprog/nil) (wstack/cons Psi W1) Xi (wstack/cons Psi W2)
             -> cor-bs V W1 -> cor-bs V W2 -> type.
%mode coerce-cor-bs +DS +DC -DC'.

- : coerce-cor-bs sstep/ret DC DC.

%worlds () (coerce-cor-bs _ _ _).
%total {} (coerce-cor-bs _ _ _).



coerce-beval-num : cor-bs (vnum N1) (wnum N2) -> beval Sigma B (vnum N1) -> beval Sigma B (vnum N2) -> type.
%mode coerce-beval-num +DC +DB -DB'.

- : coerce-beval-num cor-bs/num DB DB.

%worlds () (coerce-beval-num _ _ _).
%total {} (coerce-beval-num _ _ _).



soundness-bs : seval P (wnum N) -> trans-bs B P -> beval store/nil B (vnum N) -> type.
%mode soundness-bs +DP +DT -DB.

soundness-bs/ : soundness-bs (seval/ DS*) DT DB'
                 <- soundness-bs' DS* DT comp-bs/nil DB DC (ssteps/single DS)
                 <- coerce-cor-bs DS DC DC'
                 <- coerce-beval-num DC' DB DB'.

%worlds () (soundness-bs _ _ _).
%total {} (soundness-bs _ _ _).
