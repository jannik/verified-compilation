eq-value : value -> value -> type.

eq-value/refl : eq-value U U.



eq-svalue : svalue -> svalue -> type.

eq-svalue/refl : eq-svalue V V.



invert-ssteps-suc : ssteps (frames/cons Gamma Bet (sprog/cons ssuc P)) (wstack/cons Psi V) frames/nil Psi'
                 -> eq-svalue V (wnum N)
                 -> ssteps (frames/cons Gamma Bet P) (wstack/cons Psi (wnum (s N))) frames/nil Psi'
                 -> type.
%mode invert-ssteps-suc +DP -DQ1 -DP'.

invert-ssteps-suc/ : invert-ssteps-suc (ssteps/more DP sstep/suc) eq-svalue/refl DP.

%worlds () (invert-ssteps-suc _ _ _).
%reduces DP' < DP (invert-ssteps-suc DP _ DP').
%total {} (invert-ssteps-suc _ _ _).



invert-cor-bs-num : cor-bs U V -> eq-svalue V (wnum N) -> eq-value U (vnum N) -> type.
%mode invert-cor-bs-num +DC +DQV -DQU.

invert-cor-bs-num/ : invert-cor-bs-num cor-bs/num eq-svalue/refl eq-value/refl.

%worlds () (invert-cor-bs-num _ _ _).
%total {} (invert-cor-bs-num _ _ _).



invert-cor-bs-fun : cor-bs U V -> eq-svalue V (wclos Bet P)
                 -> eq-value U (vclos Alph B) -> trans-bs B P -> comp-bs Alph Bet
                 -> type.
%mode invert-cor-bs-fun +DC +DQV -DQU -DT -DD.

invert-cor-bs-fun/ : invert-cor-bs-fun (cor-bs/fun DT DD) eq-svalue/refl eq-value/refl DT DD.

%worlds () (invert-cor-bs-fun _ _ _ _ _).
%total {} (invert-cor-bs-fun _ _ _ _ _).



invert-ssteps-app : ssteps (frames/cons Gamma Bet (sprog/cons sapp P)) (wstack/cons (wstack/cons Psi V1) V2)
                           frames/nil Psi'
                 -> eq-svalue V1 (wclos Bet' P') -> ssteps (frames/cons (frames/cons Gamma Bet P)
                              (senv/cons Bet' V2) P') Psi frames/nil Psi' -> type.
%mode invert-ssteps-app +DP -DQ -DP'.

invert-ssteps-app/ : invert-ssteps-app (ssteps/more DP sstep/app) eq-svalue/refl DP.

%worlds () (invert-ssteps-app _ _ _).
%reduces DP' < DP (invert-ssteps-app DP _ DP').
%total {} (invert-ssteps-app _ _ _).



coerce-beval : beval Alph B U -> eq-value U U' -> beval Alph B U' -> type.
%mode coerce-beval +DB +DQU -DB'.

coerce-beval/ : coerce-beval DB eq-value/refl DB.

%worlds () (coerce-beval _ _ _).
%total {} (coerce-beval _ _ _).



soundness-bs-var : seval-var Bet I V -> comp-bs Alph Bet -> beval-var Alph I U -> cor-bs U V -> type.
%mode soundness-bs-var +DSV +DD -DBV -DC.

soundness-bs-var/here : soundness-bs-var seval-var/here (comp-bs/cons DC DD) beval-var/here DC.

soundness-bs-var/there : soundness-bs-var (seval-var/there DSV) (comp-bs/cons DC' DD) (beval-var/there DBV) DC
                          <- soundness-bs-var DSV DD DBV DC.

%worlds () (soundness-bs-var _ _ _ _).
%total DSV (soundness-bs-var DSV _ _ _).



soundness-bs' : ssteps (frames/cons Gamma Bet P1-P2) Psi frames/nil (wstack/cons wstack/nil V')
             -> trans-bs-acc B P2 P1-P2
             -> comp-bs Alph Bet
             -> beval Alph B U
             -> cor-bs U V
             -> ssteps (frames/cons Gamma Bet P2) (wstack/cons Psi V) frames/nil (wstack/cons wstack/nil V')
             -> type.
%mode soundness-bs' +DP +DT +DD -DB -DC -DP'.

soundness-bs'/num : soundness-bs' (ssteps/more DP sstep/num) trans-bs-acc/num DD beval/num cor-bs/num DP.

soundness-bs'/var : soundness-bs' (ssteps/more DP (sstep/var DSV)) trans-bs-acc/var DD (beval/var DBV) DC DP
                     <- soundness-bs-var DSV DD DBV DC.

soundness-bs'/lam : soundness-bs' (ssteps/more DP sstep/lam) (trans-bs-acc/lam DT1) DD beval/lam (cor-bs/fun DT1 DD) DP.

soundness-bs'/app : soundness-bs' DP (trans-bs-acc/app DT1 DT2) DD (beval/app DB3 DB2 DB1') DC DP4
                     <- soundness-bs' DP DT1 DD DB1 DC1 DP1
                     <- soundness-bs' DP1 DT2 DD DB2 DC2 DP2
                     <- invert-ssteps-app DP2 DQV DP3
                     <- invert-cor-bs-fun DC1 DQV DQU DT3 DD'
                     <- coerce-beval DB1 DQU DB1'
                     <- soundness-bs' DP3 DT3 (comp-bs/cons DC2 DD') DB3 DC (ssteps/more DP4 sstep/ret).

soundness-bs'/suc : soundness-bs' DP (trans-bs-acc/suc DT1) DD (beval/suc DB1') cor-bs/num DP1'
                     <- soundness-bs' DP DT1 DD DB1 DC1 DP1
                     <- invert-ssteps-suc DP1 DQV DP1'
                     <- invert-cor-bs-num DC1 DQV DQU
                     <- coerce-beval DB1 DQU DB1'.

%worlds () (soundness-bs' _ _ _ _ _ _).
%reduces DP' < DP (soundness-bs' DP _ _ _ _ DP').
%total {DP DT} (soundness-bs' DP DT _ _ _ _).



coerce-cor-bs : sstep (frames/cons Gamma Bet sprog/nil) (wstack/cons Psi V1) Gamma (wstack/cons Psi V2)
             -> cor-bs U V1 -> cor-bs U V2 -> type.
%mode coerce-cor-bs +DS +DC -DC'.

- : coerce-cor-bs sstep/ret DC DC.

%worlds () (coerce-cor-bs _ _ _).
%total {} (coerce-cor-bs _ _ _).



coerce-beval-num : cor-bs (vnum N1) (wnum N2) -> beval Alph B (vnum N1) -> beval Alph B (vnum N2) -> type.
%mode coerce-beval-num +DC +DB -DB'.

- : coerce-beval-num cor-bs/num DB DB.

%worlds () (coerce-beval-num _ _ _).
%total {} (coerce-beval-num _ _ _).



soundness-bs : seval P (wnum N) -> trans-bs B P -> beval env/nil B (vnum N) -> type.
%mode soundness-bs +DP +DT -DB.

soundness-bs/ : soundness-bs (seval/ DS*) DT DB'
                 <- soundness-bs' DS* DT comp-bs/nil DB DC (ssteps/single DS)
                 <- coerce-cor-bs DS DC DC'
                 <- coerce-beval-num DC' DB DB'.

%worlds () (soundness-bs _ _ _).
%total {} (soundness-bs _ _ _).
