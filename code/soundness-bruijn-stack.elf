cor-bs : value -> svalue -> type. %name cor-bs DC.
comp-bs : store -> sstore -> type. %name comp-bs DD.

cor-bs/num : cor-bs (vnum N) (wnum N).

cor-bs/fun : cor-bs (vclos Sigma B) (wclos Tau P)
              <- comp-bs Sigma Tau
              <- trans-bs B P.

comp-bs/nil : comp-bs store/nil sstore/nil.

comp-bs/cons : comp-bs (store/cons Sigma V) (sstore/cons Tau W)
                <- comp-bs Sigma Tau
                <- cor-bs V W.



soundness-bs-var : beval-var Sigma I V -> comp-bs Sigma Tau
                -> seval-var Tau I W -> cor-bs V W -> type.
%mode soundness-bs-var +DBV +DD -DSV -DC.

soundness-bs-var/here : soundness-bs-var beval-var/here (comp-bs/cons DC DD) seval-var/here DC.

soundness-bs-var/there : soundness-bs-var (beval-var/there DBV) (comp-bs/cons DC' DD) (seval-var/there DSV) DC
                          <- soundness-bs-var DBV DD DSV DC.

%worlds () (soundness-bs-var _ _ _ _).
%total DBV (soundness-bs-var DBV _ _ _).



ssteps/single : sstep Xi Psi Xi' Psi' -> ssteps Xi Psi Xi' Psi' = [ds] ssteps/more ssteps/zero ds.



cat-ssteps : ssteps Xi Psi Xi' Psi' -> ssteps Xi' Psi' Xi'' Psi'' -> ssteps Xi Psi Xi'' Psi'' -> type.
%mode cat-ssteps +DP1 +DP2 -DP3.

cat-ssteps/zero : cat-ssteps ssteps/zero DP2 DP2.

cat-ssteps/more : cat-ssteps (ssteps/more DP1 DS1) DP2 (ssteps/more DP3 DS1)
                   <- cat-ssteps DP1 DP2 DP3.

%worlds () (cat-ssteps _ _ _).
%total DP1 (cat-ssteps DP1 _ _).



soundness-bs' : trans-bs-acc B P2 P -> beval Sigma B V -> comp-bs Sigma Tau
             -> ssteps (frames/cons Xi Tau P) Psi (frames/cons Xi Tau P2) (wstack/cons Psi W)
             -> cor-bs V W -> type.
%mode +{B} +{P2} +{P} +{Sigma} +{V} +{Tau} +{Xi} +{Psi} -{W}
      +{DT:trans-bs-acc B P2 P} +{DB:beval Sigma B V} +{DD:comp-bs Sigma Tau}
      -{DP:ssteps (frames/cons Xi Tau P) Psi (frames/cons Xi Tau P2) (wstack/cons Psi W)}
      -{DC:cor-bs V W}
      soundness-bs' DT DB DD DP DC.

soundness-bs'/num : soundness-bs' trans-bs-acc/num beval/num DD (ssteps/single sstep/num) cor-bs/num.

soundness-bs'/var : soundness-bs' trans-bs-acc/var (beval/var DBV) DD
                                  (ssteps/single (sstep/var DSV)) DC
                     <- soundness-bs-var DBV DD DSV DC.

soundness-bs'/lam : soundness-bs' (trans-bs-acc/lam DT) beval/lam DD
                                  (ssteps/single sstep/lam) (cor-bs/fun DT DD).

soundness-bs'/app : soundness-bs' (trans-bs-acc/app DT1 DT2) (beval/app DB3 DB2 DB1) DD DP DC
                     <- soundness-bs' DT1 DB1 DD DP1 (cor-bs/fun DT0 DD0)
                     <- soundness-bs' DT2 DB2 DD DP2 DC2
                     <- soundness-bs' DT0 DB3 (comp-bs/cons DC2 DD0) DP3 DC
                     <- cat-ssteps DP1 DP2 DP12
                     <- cat-ssteps DP12 (ssteps/more DP3 sstep/app) DP123
                     <- cat-ssteps DP123 (ssteps/more ssteps/zero sstep/ret) DP.

%worlds () (soundness-bs' _ _ _ _ _).
%total DB (soundness-bs' _ DB _ _ _).



coerce-seval-num : cor-bs (vnum N1) (wnum N2) -> seval P (wnum N2) -> seval P (wnum N1) -> type.
%mode coerce-seval-num +DC +DP -DP'.

- : coerce-seval-num cor-bs/num DP DP.

%worlds () (coerce-seval-num _ _ _).
%total (DC) (coerce-seval-num DC _ _).



soundness-bs : beval store/nil B (vnum N) -> trans-bs B P -> seval P (wnum N) -> type.
%mode soundness-bs +DB +DT -DP.

- : soundness-bs DB DT DP
     <- soundness-bs' DT DB comp-bs/nil DS* DC
     <- cat-ssteps DS* (ssteps/more ssteps/zero sstep/ret) DS*'
     <- coerce-seval-num DC (seval/ DS*') DP.

%worlds () (soundness-bs _ _ _).
%total DB (soundness-bs DB _ _).
