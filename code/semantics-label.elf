
mvalue : type. %name mvalue R.
mstore : type. %name mstore Alpha.

rnum : number -> mvalue.
rclos : mstore -> label -> mvalue.

mstore/nil : mstore.
mstore/cons : mstore -> mvalue -> mstore.



rstack : type. %name rstack Phi.

rstack/nil : rstack.
rstack/cons : rstack -> mvalue -> rstack.



rstack/single = rstack/cons rstack/nil.



mframes : type. %name mframes Gamma.

mframes/nil : mframes.
mframes/cons : mframes -> mstore -> label -> mframes.

mframes/final = mframes/cons mframes/nil mstore/nil.



mvar-lookup : mstore -> nat -> mvalue -> type. %name mvar-lookup DVL.
%mode mvar-lookup +Alpha +I -R.

mvar-lookup/here : mvar-lookup (mstore/cons Alpha R) z R.

mvar-lookup/there : mvar-lookup (mstore/cons Alpha R') (s I) R
                     <- mvar-lookup Alpha I R.



label-lookup : mprog -> label -> mblock -> type. %name label-lookup DLL.
%mode label-lookup +Q +L -B.

label-lookup/here : label-lookup (mprog/cons B Q) here B.

label-lookup/next : label-lookup (mprog/cons B' Q) (next L) B
                     <- label-lookup Q L B.

label-lookup/left : label-lookup (mprog/node Q1 Q2) (left L) B
                     <- label-lookup Q1 L B.

label-lookup/right : label-lookup (mprog/node Q1 Q2) (right L) B
                     <- label-lookup Q2 L B.

label-lookup/done : label-lookup Q done mhalt.



mstep : mprog -> mframes -> mblock -> rstack -> mframes -> mblock -> rstack -> type. %name mstep DM.
%mode mstep +Q +Gamma +B +Phi -Gamma' -B' -Phi'.

mstep/pushnum : mstep Q (mframes/cons Gamma Alpha L) (mpushnum N ; B) Phi
                        (mframes/cons Gamma Alpha L) B (rstack/cons Phi (rnum N)).
mstep/pushvar : mstep Q (mframes/cons Gamma Alpha L) (mpushvar I ; B) Phi
                        (mframes/cons Gamma Alpha L) B (rstack/cons Phi R)
                 <- mvar-lookup Alpha I R.

mstep/pushclos : mstep Q (mframes/cons Gamma Alpha L) (mpushclos L' ; B) Phi
                         (mframes/cons Gamma Alpha L) B (rstack/cons Phi (rclos Alpha L')).

mstep/call : mstep Q (mframes/cons Gamma Alpha L) (mcall L'') (rstack/cons (rstack/cons Phi (rclos Alpha' L')) R)
                     (mframes/cons (mframes/cons Gamma Alpha L) (mstore/cons Alpha' R) L'') B Phi
              <- label-lookup Q L' B.

mstep/ret : mstep Q (mframes/cons Gamma Alpha' L') mret Phi
                    Gamma B Phi
             <- label-lookup Q L' B.


msteps : mprog -> mframes -> mblock -> rstack -> mframes -> mblock -> rstack -> type. %name ssteps DMM.
%mode msteps +Q +Gamma +B +Phi -Gamma' -B' -Phi'.

msteps/zero : msteps Q Gamma B Phi Gamma B Phi.

msteps/more : msteps Q Gamma B Phi Gamma'' B'' Phi''
               <- mstep Q Gamma B Phi Gamma' B' Phi'
               <- msteps Q Gamma' B' Phi' Gamma'' B'' Phi''.



meval : mblock -> mprog -> mvalue -> type. %name meval DQ.
%mode meval +B +Q -R.

meval/ : meval B Q R
          <- msteps Q (mframes/final done) B rstack/nil
                      mframes/nil mhalt (rstack/single R).
