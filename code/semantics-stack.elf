svalue : type. %name svalue W.
sstore : type. %name sstore Bet.

wnum : nat -> svalue.
wclos : sstore -> sprog -> svalue.

sstore/nil : sstore.
sstore/cons : sstore -> svalue -> sstore.



frames : type. %name frames Gamma.

frames/nil : frames.
frames/cons : frames -> sstore -> sprog -> frames.



wstack : type. %name wstack Psi.

wstack/nil : wstack.
wstack/cons : wstack -> svalue -> wstack.

wstack/single = wstack/cons wstack/nil.



seval-var : sstore -> nat -> svalue -> type. %name seval-var DSV.
%mode seval-var +Bet +I -W.

seval-var/here : seval-var (sstore/cons Bet W) z W.

seval-var/there : seval-var (sstore/cons Bet W') (s I) W
                   <- seval-var Bet I W.



sstep : frames -> wstack -> frames -> wstack -> type. %name sstep DS.
%mode sstep +Gamma +Psi -Gamma' -Psi'.

sstep/num : sstep (frames/cons Gamma Bet (sprog/cons (snum N) P)) Psi
                  (frames/cons Gamma Bet P) (wstack/cons Psi (wnum N)).

sstep/var : sstep (frames/cons Gamma Bet (sprog/cons (svar I) P)) Psi
                  (frames/cons Gamma Bet P) (wstack/cons Psi W)
             <- seval-var Bet I W.

sstep/lam : sstep (frames/cons Gamma Bet (sprog/cons (slam P') P)) Psi
                  (frames/cons Gamma Bet P) (wstack/cons Psi (wclos Bet P')).

sstep/app : sstep (frames/cons Gamma Bet (sprog/cons sapp P))
                  (wstack/cons (wstack/cons Psi (wclos Bet' P')) W)
                  (frames/cons (frames/cons Gamma Bet P) (sstore/cons Bet' W) P')
                  Psi.

sstep/suc : sstep (frames/cons Gamma Bet (sprog/cons ssuc P)) (wstack/cons Psi (wnum N))
                  (frames/cons Gamma Bet P) (wstack/cons Psi (wnum (s N))).

sstep/ret : sstep (frames/cons Gamma Bet sprog/nil) Psi
                  Gamma Psi.


ssteps : frames -> wstack -> frames -> wstack -> type. %name ssteps DP.
%mode ssteps +Gamma +Psi -Gamma' -Psi'.

ssteps/zero : ssteps Gamma Psi Gamma Psi.

ssteps/more : ssteps Gamma Psi Gamma'' Psi''
               <- sstep Gamma Psi Gamma' Psi'
               <- ssteps Gamma' Psi' Gamma'' Psi''.



seval : sprog -> svalue -> type. %name seval DP.
%mode seval +P -W.

seval/ : seval P W
          <- ssteps (frames/cons frames/nil sstore/nil P) wstack/nil
                    frames/nil (wstack/cons wstack/nil W).
