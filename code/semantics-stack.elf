svalue : type. %name svalue W.
sstore : type. %name sstore Tau.

wnum : nat -> svalue.
wclos : sstore -> sprog -> svalue.

sstore/nil : sstore.
sstore/cons : sstore -> svalue -> sstore.



frames : type. %name frames Xi.

frames/nil : frames.
frames/cons : frames -> sstore -> sprog -> frames.



wstack : type. %name wstack Psi.

wstack/nil : wstack.
wstack/cons : wstack -> svalue -> wstack.

wstack/single = wstack/cons wstack/nil.



seval-var : sstore -> nat -> svalue -> type. %name seval-var DSV.
%mode seval-var +Tau +I -W.

seval-var/here : seval-var (sstore/cons Tau W) z W.

seval-var/there : seval-var (sstore/cons Tau W') (s I) W
                   <- seval-var Tau I W.



sstep : frames -> wstack -> frames -> wstack -> type. %name sstep DS.
%mode sstep +Xi +Psi -Xi' -Psi'.

sstep/num : sstep (frames/cons Xi Tau (sprog/cons (snum N) P)) Psi
                  (frames/cons Xi Tau P) (wstack/cons Psi (wnum N)).

sstep/var : sstep (frames/cons Xi Tau (sprog/cons (svar I) P)) Psi
                  (frames/cons Xi Tau P) (wstack/cons Psi W)
             <- seval-var Tau I W.

sstep/lam : sstep (frames/cons Xi Tau (sprog/cons (slam P') P)) Psi
                  (frames/cons Xi Tau P) (wstack/cons Psi (wclos Tau P')).

sstep/app : sstep (frames/cons Xi Tau (sprog/cons sapp P))
                  (wstack/cons (wstack/cons Psi (wclos Tau' P')) W)
                  (frames/cons (frames/cons Xi Tau P) (sstore/cons Tau' W) P')
                  Psi.

sstep/suc : sstep (frames/cons Xi Tau (sprog/cons ssuc P)) (wstack/cons Psi (wnum N))
                  (frames/cons Xi Tau P) (wstack/cons Psi (wnum (s N))).

sstep/case/z : sstep (frames/cons Xi Tau (sprog/cons (scase P1 P2) P))
                     (wstack/cons Psi (wnum z))
                     (frames/cons (frames/cons Xi Tau P) Tau P1)
                     Psi.

sstep/case/s : sstep (frames/cons Xi Tau (sprog/cons (scase P1 P2) P))
                     (wstack/cons Psi (wnum (s N)))
                     (frames/cons (frames/cons Xi Tau P) (sstore/cons Tau (wnum N)) P2)
                     Psi.

sstep/ret : sstep (frames/cons Xi Tau sprog/nil) Psi
                  Xi Psi.



ssteps : frames -> wstack -> frames -> wstack -> type. %name ssteps DP.
%mode ssteps +Xi +Psi -Xi' -Psi'.

ssteps/zero : ssteps Xi Psi Xi Psi.

ssteps/more : ssteps Xi Psi Xi'' Psi''
               <- sstep Xi Psi Xi' Psi'
               <- ssteps Xi' Psi' Xi'' Psi''.



seval : sprog -> svalue -> type. %name seval DP.
%mode seval +P -W.

seval/ : seval P W
          <- ssteps (frames/cons frames/nil sstore/nil P) wstack/nil
                    frames/nil (wstack/cons wstack/nil W).
