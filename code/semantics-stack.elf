svalue : type. %name svalue V.
senv : type. %name senv Bet.

wnum : nat -> svalue.
wclos : senv -> sprog -> svalue.

senv/nil : senv.
senv/cons : senv -> svalue -> senv.



frames : type. %name frames Gamma.

frames/nil : frames.
frames/cons : frames -> senv -> sprog -> frames.



wstack : type. %name wstack Psi.

wstack/nil : wstack.
wstack/cons : wstack -> svalue -> wstack.

wstack/single = wstack/cons wstack/nil.



seval-var : senv -> nat -> svalue -> type. %name seval-var DSV.
%mode seval-var +Bet +I -V.

seval-var/here : seval-var (senv/cons Bet V) z V.

seval-var/there : seval-var (senv/cons Bet V') (s I) V
                   <- seval-var Bet I V.



sstep : frames -> wstack -> frames -> wstack -> type. %name sstep DS.
%mode sstep +Gamma +Psi -Gamma' -Psi'.

sstep/num : sstep (frames/cons Gamma Bet (sprog/cons (snum N) P)) Psi
                  (frames/cons Gamma Bet P) (wstack/cons Psi (wnum N)).

sstep/var : sstep (frames/cons Gamma Bet (sprog/cons (svar I) P)) Psi
                  (frames/cons Gamma Bet P) (wstack/cons Psi V)
             <- seval-var Bet I V.

sstep/lam : sstep (frames/cons Gamma Bet (sprog/cons (slam P') P)) Psi
                  (frames/cons Gamma Bet P) (wstack/cons Psi (wclos Bet P')).

sstep/app : sstep (frames/cons Gamma Bet (sprog/cons sapp P))
                  (wstack/cons (wstack/cons Psi (wclos Bet' P')) V)
                  (frames/cons (frames/cons Gamma Bet P) (senv/cons Bet' V) P')
                  Psi.

sstep/suc : sstep (frames/cons Gamma Bet (sprog/cons ssuc P)) (wstack/cons Psi (wnum N))
                  (frames/cons Gamma Bet P) (wstack/cons Psi (wnum (s N))).

sstep/ret : sstep (frames/cons Gamma Bet sprog/nil) Psi
                  Gamma Psi.


ssteps : frames -> wstack -> frames -> wstack -> type. %name ssteps DP.
%mode ssteps +Gamma +Psi -Gamma' -Psi'.

ssteps/zero : ssteps Gamma Psi Gamma Psi.

ssteps/more : ssteps Gamma Psi Gamma'' Psi''
               <- sstep Gamma Psi Gamma' Psi'
               <- ssteps Gamma' Psi' Gamma'' Psi''.



seval : sprog -> svalue -> type. %name seval DP.
%mode seval +P -V.

seval/ : seval P V
          <- ssteps (frames/cons frames/nil senv/nil P) wstack/nil
                    frames/nil (wstack/cons wstack/nil V).
