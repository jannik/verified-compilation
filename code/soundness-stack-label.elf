leq : mprog -> mprog -> (label -> label) -> type. %name leq DLE.

leq/left : leq Q' (mprog/node Q1 Q2) left
            <- shift-mprog left Q' Q1.

leq/right : leq Q' (mprog/node Q1 Q2) right
             <- shift-mprog right Q' Q2.

leq/next : leq Q' (mprog/cons B Q1) next
            <- shift-mprog next Q' Q1.

leq/refl : leq Q' Q' ([l] l).

leq/trans : leq Q' Q (G o F)
             <- leq Q' Q'' F
             <- leq Q'' Q G.



shift-mblock-refl : shift-mblock ([l] l) B B -> type.
%mode +{B : mblock} -{DH : shift-mblock ([l] l) B B} shift-mblock-refl DH.

%worlds () (shift-mblock-refl _).
%trustme %total {} (shift-mblock-refl _).



shift-mblock-trans : shift-mblock F B B' -> shift-mblock G B' B'' -> shift-mblock (G o F) B B'' -> type.
%mode shift-mblock-trans +DH +DH' -DH''.

%worlds () (shift-mblock-trans _ _ _).
%trustme %total {} (shift-mblock-trans _ _ _).



shift-mprog-trans : shift-mprog F Q Q' -> shift-mprog G Q' Q'' -> shift-mprog (G o F) Q Q'' -> type.
%mode shift-mprog-trans +DH +DH' -DH''.

%worlds () (shift-mprog-trans _ _ _).
%trustme %total {} (shift-mprog-trans _ _ _).



lookup-respects-shift : label-lookup Q' L B' -> shift-mprog F Q' Q -> label-lookup Q L B -> shift-mblock F B' B -> type.
%mode lookup-respects-shift +DLL +DH -DLL' -DH'.

%worlds () (lookup-respects-shift _ _ _ _).
%trustme %total {} (lookup-respects-shift _ _ _ _).



lookup-respects-leq : leq Q' Q F -> label-lookup Q' L B' -> label-lookup Q (F L) B -> shift-mblock F B' B -> type.
%mode lookup-respects-leq +DLE +DLL -DLL' -DH'.

lookup-respects-leq/left
   : lookup-respects-leq (leq/left DH : leq Q' (mprog/node Q1 Q2) left) (DLL : label-lookup Q' L B')
                         (label-lookup/left (DLL' : label-lookup Q1 L B)) DH'
      <- lookup-respects-shift DLL DH DLL' DH'.

lookup-respects-leq/right
   : lookup-respects-leq (leq/right DH) DLL (label-lookup/right DLL') DH'
      <- lookup-respects-shift DLL DH DLL' DH'.

lookup-respects-leq/next
   : lookup-respects-leq (leq/next DH) DLL (label-lookup/next DLL') DH'
      <- lookup-respects-shift DLL DH DLL' DH'.

lookup-respects-leq/refl
   : lookup-respects-leq leq/refl DLL DLL DH
      <- shift-mblock-refl DH.

lookup-respects-leq/trans
   : lookup-respects-leq (leq/trans DLE2 DLE1) DLL DLL' DH
      <- lookup-respects-leq DLE1 DLL DLL'' DH''
      <- lookup-respects-leq DLE2 DLL'' DLL' DH'
      <- shift-mblock-trans DH'' DH' DH.

%worlds () (lookup-respects-leq _ _ _ _).
%total DLE (lookup-respects-leq DLE _ _ _).



cor-value : mprog -> svalue -> mvalue -> type.

cor-store : mprog -> sstore -> mstore -> type.

% cor-value

cor-value/num : cor-value Q (wnum N) (rnum N).

cor-value/clos : cor-value Q (wclos Tau P) (rclos Alpha L)
                  <- cor-store Q Tau Alpha
                  <- label-lookup Q L B
                  <- trans-sm P B' Q'
                  <- leq Q' Q F
                  <- shift-mblock F B' B.

% cor-store

cor-store/nil : cor-store Q sstore/nil mstore/nil.

cor-store/cons : cor-store Q (sstore/cons Tau W) (mstore/cons Alpha R)
                  <- cor-store Q Tau Alpha
                  <- cor-value Q W R.



cor-stack : mprog -> frames -> mblock -> mframes -> type.

cor-stack/nil : cor-stack Q frames/nil mhalt mframes/nil.

cor-stack/cons : cor-stack Q (frames/cons Xi Tau P) B (mframes/cons Gamma Alpha L)
                  <- cor-stack Q Xi B' Gamma
                  <- shift-mblock F B'' B
                  <- cor-store Q Tau Alpha
                  <- label-lookup Q L B'
                  <- trans-sm P B'' Q'
                  <- leq Q' Q F.



cor-value-stack : mprog -> wstack -> rstack -> type.

cor-value-stack/nil : cor-value-stack Q wstack/nil rstack/nil.

cor-value-stack/cons : cor-value-stack Q (wstack/cons Psi W) (rstack/cons Phi R)
                        <- cor-value-stack Q Psi Phi
                        <- cor-value Q W R.



label-lookup-unique : label-lookup Q L B -> label-lookup Q L B' -> eq-mblock B B' -> type.
%mode label-lookup-unique +DLL +DLL' -EQ.

label-lookup-unique/here : label-lookup-unique label-lookup/here label-lookup/here eq-mblock/refl.



%worlds () (label-lookup-unique _ _ _).
%trustme %total DLL (label-lookup-unique DLL _ _).



coerce-cor-stack : cor-stack Q Xi B Gamma -> eq-mblock B B' -> cor-stack Q Xi B' Gamma -> type.
%mode coerce-cor-stack +DCS +EQ -DCS'.

coerce-cor-stack/ : coerce-cor-stack DCS eq-mblock/refl DCS.

%worlds () (coerce-cor-stack _ _ _).
%total {} (coerce-cor-stack _ _ _).



soundness-sm-var : mvar-lookup Alpha I R -> cor-store Q Tau Alpha -> seval-var Tau I W -> cor-value Q W R -> type.
%mode soundness-sm-var +DVL +DCE -DSV -DC.

soundness-sm-var/here : soundness-sm-var mvar-lookup/here (cor-store/cons DC DCE) seval-var/here DC.

soundness-sm-var/there : soundness-sm-var (mvar-lookup/there DVL) (cor-store/cons DC' DCE) (seval-var/there DSV) DC
                          <- soundness-sm-var DVL DCE DSV DC.

%worlds () (soundness-sm-var _ _ _ _).
%total DVL (soundness-sm-var DVL _ _ _).



soundness-sm'
   : cor-value-stack Q Psi Phi
      -> cor-stack Q Xi B Gamma
      -> msteps Q Gamma B Phi mframes/nil mhalt (rstack/single R)
      -> ssteps Xi Psi frames/nil (wstack/single W)
      -> cor-value Q W R
      -> type.
%mode soundness-sm' +DMM +DCS +DCV -DP -DC.

soundness-sm'/halt
   : soundness-sm' (cor-value-stack/cons DC cor-value-stack/nil) cor-stack/nil msteps/zero
                   ssteps/zero DC.

soundness-sm'/num
   : soundness-sm' DCV (cor-stack/cons DLE (trans-sm/num DT) DLL DCE (shift-mblock/cons DH shift-minst/pushnum) DCS) (msteps/more DMM mstep/pushnum)
                   (ssteps/more DP sstep/num) DC
      <- soundness-sm' (cor-value-stack/cons cor-value/num DCV) (cor-stack/cons DLE DT DLL DCE DH DCS) DMM DP DC.

soundness-sm'/var
   : soundness-sm' DCV (cor-stack/cons DLE (trans-sm/var DT) DLL DCE (shift-mblock/cons DH shift-minst/pushvar) DCS) (msteps/more DMM (mstep/pushvar DVL))
                   (ssteps/more DP (sstep/var DSV)) DC
      <- soundness-sm-var DVL DCE DSV DC'
      <- soundness-sm' (cor-value-stack/cons DC' DCV) (cor-stack/cons DLE DT DLL DCE DH DCS) DMM DP DC.

soundness-sm'/lam
   : soundness-sm' DCV
                   (cor-stack/cons
                      (DLE : leq (mprog/node Q' (mprog/cons BB' QQ')) Q1 F)
                      (trans-sm/lam
                         (shift-mprog/cons DHH DBB : shift-mprog right (mprog/cons BB QQ) (mprog/cons BB' QQ'))
                         (DH : shift-mprog left Q Q')
                         (DHB : shift-mblock left B B')
                         (trans-sm-fun/ DH1 DH2 DTT : trans-sm-fun PP (mprog/cons BB QQ)) (DT : trans-sm P B Q))
                      DLL
                      DCE 
                      (shift-mblock/cons DH'' shift-minst/pushclos)
                      DCS)
                   (msteps/more (DMM : msteps Q1 (mframes/cons Gamma Alpha L) B _ _ _ _) mstep/pushclos)
                   (ssteps/more DP sstep/lam)
                   DC
      <- shift-mblock-trans DHB DH'' DH'''
      <- lookup-respects-leq DLE (label-lookup/right label-lookup/here) DLLL DH4
      <- shift-mprog-trans DH2 DHH DH47
      <- shift-mblock-trans DH1 DBB DHHH
      <- shift-mblock-trans DHHH DH4 DHHH'
      <- soundness-sm'
         (cor-value-stack/cons
            (cor-value/clos
               (DHHH')
               (leq/trans DLE (leq/trans (leq/right (shift-mprog/cons DHH DBB)) (leq/next DH2)) : leq Q2 Q1 (F o right o next))
               DTT DLLL DCE)
            DCV)
         (cor-stack/cons (leq/trans DLE (leq/left DH)) DT DLL DCE DH''' DCS) DMM
         DP
         DC.

soundness-sm'/app
   : soundness-sm'
      (cor-value-stack/cons DC' (cor-value-stack/cons (cor-value/clos DH' (DLE') (DT') DLL' DCE') DCV))
      (cor-stack/cons
         (DLE)
         (trans-sm/app DH1 DH2 DT)
         (DLL)
         DCE
         shift-mblock/call
         DCS)
      (msteps/more (DMM) (mstep/call DLL2))
      (ssteps/more DP sstep/app)
      DC
      <- lookup-respects-leq DLE label-lookup/here DLL3 DH3
      <- shift-mblock-trans DH2 DH3 DH4
      <- label-lookup-unique DLL' DLL2 EQ
      <- coerce-cor-stack
         (cor-stack/cons
            DLE'
            DT'
            DLL3
            (cor-store/cons DC' DCE')
            DH'
            (cor-stack/cons
               (leq/trans DLE (leq/next DH1))
               DT
               (DLL)
               DCE
               DH4
               DCS))
         EQ
         DCS'
      <- soundness-sm'
         DCV
         DCS'
         DMM
         DP
         DC.

soundness-sm'/ret
   : soundness-sm'
      DCV
      (cor-stack/cons DLE trans-sm/ret DLL' DCE shift-mblock/ret DCS)
      (msteps/more DMM (mstep/ret DLL))
      (ssteps/more DP sstep/ret) DC
      <- label-lookup-unique DLL' DLL EQ
      <- coerce-cor-stack DCS EQ DCS'
      <- soundness-sm' DCV DCS' DMM DP DC.

%worlds () (soundness-sm' _ _ _ _ _).
%total DMM (soundness-sm' _ _ DMM _ _).



soundness-sm : trans-sm P B Q -> meval B Q (rnum N) -> seval P (wnum N) -> type.
%mode soundness-sm +DT +DQ -DP.

soundness-sm/ : soundness-sm DT (meval/ DMM) (seval/ DP)
                 <- shift-mblock-refl DH
                 <- soundness-sm' (cor-stack/cons leq/refl DT label-lookup/done cor-store/nil DH cor-stack/nil) DMM cor-value-stack/nil
                                  DP cor-value/num.
